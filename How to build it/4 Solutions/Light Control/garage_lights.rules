/*
This rule changes the scene automatically when either
- presence is detected while Luminance is low 
- the manual timer has been set
-> a change in the scene will trigger the next rule...
*/
rule "Garage: Change light scene automatically"
when 
    Item G_Garage_Presence changed or
    Item garage_Sensor1_Luminance changed or
    Item garage_Settings_LuminanceLimit changed or
    Item garage_Lights_Manual changed to 0
then 
    if ( garage_Lights_Manual.state > 0 ){
        //manual settings are still active, nothing is done automatically
    }else{

        if( G_Garage_Presence.state == ON &&                                        //Presence
        garage_Sensor1_Luminance.state < garage_Settings_LuminanceLimit.state ){  //with poor light
            
            //Change the scene:
            switch(global_tageszeit.state){
                case 'MORNING': garage_Lights_Scene.postUpdate( 'WAKEUP' )
                case 'DAY':     garage_Lights_Scene.postUpdate( 'BRIGHT' )
                case 'EVENING': garage_Lights_Scene.postUpdate( 'CHILL'  )
                default:        garage_Lights_Scene.postUpdate( 'BRIGHT' )
            }

        }else{
            garage_Lights_Scene.postUpdate( 'ALL_OFF' )
        }
    }
end

/* 
Lightscene can be changed manually and then, the manual timer should be initialized.
Updates by the automatic rule is done using "postUpdate" and will not trigger this initialization 
*/ 
rule "Garage: Lightscene changes actual Lights"
when
    Item garage_Lights_Scene received command
then
    if(receivedCommand == 'ALL_OFF' ){
        garage_Lights_Manual.postUpdate(0)
    }else{
        garage_Lights_Manual.postUpdate(20)
    }
end

/*
When the lightscene changes, we can set all the lights and their values here
depending on the selected scene 
*/
rule "Garage: Lightscene changes actual Lights"
when
    Item garage_Lights_Scene changed
then
    switch (garage_Lights_Scene.state){
        case 'WAKEUP': { 
            G_Garage_LightsSwitches.sendCommand( ON )
            G_Garage_LightsColor.sendCommand( global_lightmode_energetic.state )
        }
        case 'BRIGHT': { 
            G_Garage_LightsSwitches.sendCommand( ON )
            G_Garage_LightsColor.sendCommand( global_lightmode_warm.state )
        }
        case 'CHILL':  {
            G_Garage_LightsSwitches.sendCommand( ON )
            G_Garage_LightsColor.sendCommand( global_lightmode_color.state )
        }
        case 'ALL_OFF': {
            G_Garage_LightsSwitches.sendCommand( OFF )
        }
    }

end

